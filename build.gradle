plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id "org.checkerframework" version "0.6.15" //https://github.com/kelloggm/checkerframework-gradle-plugin
}
if (project.hasProperty('ossrhUsername')) {
    apply from: 'publish.gradle'
}

group 'com.infomaximum'
version '1.1.3p12'

ext.moduleName = 'com.infomaximum.network'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

compileJava {
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath
        ]
        classpath = files()
    }
}

sourceSets {
    main {
        java.srcDir 'src/main/java'
        resources.srcDir 'src/main/resources'
    }
    test {
        java.srcDir 'src/test/java'
        resources.srcDir 'src/test/resources'
    }
    testintegration {
        java.srcDir 'src/testintegration/java'
        resources.srcDir 'src/testintegration/resources'
        compileClasspath += sourceSets.main.output + sourceSets.test.output
        runtimeClasspath += sourceSets.main.output + sourceSets.test.output
    }
    testperformance {
        java.srcDir 'src/testperformance/java'
        resources.srcDir 'src/testperformance/resources'
        compileClasspath += sourceSets.main.output + sourceSets.test.output + testintegration.output
        runtimeClasspath += sourceSets.main.output + sourceSets.test.output + testintegration.output
    }
}

configurations {
    testintegrationImplementation.extendsFrom(testImplementation)
    testintegrationRuntimeOnly.extendsFrom(testRuntimeOnly)
    testperformanceImplementation.extendsFrom(testintegrationImplementation)
    testperformanceRuntimeOnly.extendsFrom(testintegrationRuntimeOnly)
    all {
        resolutionStrategy {
            exclude group: 'org.eclipse.jetty', module: 'jetty-slf4j-impl'
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    api 'org.eclipse.jetty:jetty-server:11.0.24'
    implementation 'org.eclipse.jetty.http3:http3-server:11.0.24'
    implementation 'org.eclipse.jetty:jetty-webapp:11.0.24'
    implementation 'org.eclipse.jetty:jetty-servlets:11.0.24'

    implementation 'org.eclipse.jetty.websocket:websocket-jetty-server:11.0.24'
    api 'org.eclipse.jetty.websocket:websocket-jetty-api:11.0.24'
    implementation 'org.eclipse.jetty.websocket:websocket-servlet:11.0.24'

    api 'org.springframework:spring-core:6.0.23'
    api 'org.springframework:spring-web:6.0.23'
    api 'org.springframework:spring-webmvc:6.0.23'
    implementation 'org.springframework:spring-context-support:6.0.23'
    implementation 'org.springframework:spring-messaging:6.0.23'

    implementation 'com.infomaximum:json-smart:2.4.8'

    implementation('org.reflections:reflections:0.10.2') {
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
        exclude group: 'org.javassist', module: 'javassist'
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
    implementation 'org.javassist:javassist:3.29.2-GA'//Более новая зависимость с поддержкой java модулей

    implementation 'org.slf4j:slf4j-api:2.0.9'

    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
    testImplementation 'ch.qos.logback:logback-core:1.4.11'
    testImplementation 'ch.qos.logback:logback-classic:1.4.11'

    testintegrationImplementation 'org.springframework:spring-test:6.0.23'
    testintegrationImplementation 'org.eclipse.jetty.websocket:websocket-jetty-client:11.0.24'
    testintegrationImplementation 'org.eclipse.jetty.http3:http3-client:11.0.24'
}

task testintegration(type: Test, description: 'Integration test', group: 'Verification') {
    outputs.upToDateWhen { false }
}

task testperformance(type: Test, description: 'Performance test', group: 'Verification') {
    outputs.upToDateWhen { false }
}

tasks.withType(Copy).all { duplicatesStrategy 'exclude' }