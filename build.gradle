plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id "org.checkerframework" version "0.6.46" //https://github.com/kelloggm/checkerframework-gradle-plugin
}
if (project.hasProperty('ossrhUsername')) {
    apply from: 'publish.gradle'
}

group 'com.infomaximum'
version '1.2.1'

ext.moduleName = 'com.infomaximum.network'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

compileJava {
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath
        ]
        classpath = files()
    }
}

sourceSets {
    main {
        java.srcDir 'src/main/java'
        resources.srcDir 'src/main/resources'
    }
    test {
        java.srcDir 'src/test/java'
        resources.srcDir 'src/test/resources'
    }
    testintegration {
        java.srcDir 'src/testintegration/java'
        resources.srcDir 'src/testintegration/resources'
        compileClasspath += sourceSets.main.output + sourceSets.test.output
        runtimeClasspath += sourceSets.main.output + sourceSets.test.output
    }
    testperformance {
        java.srcDir 'src/testperformance/java'
        resources.srcDir 'src/testperformance/resources'
        compileClasspath += sourceSets.main.output + sourceSets.test.output + testintegration.output
        runtimeClasspath += sourceSets.main.output + sourceSets.test.output + testintegration.output
    }
}

configurations {
    testintegrationImplementation.extendsFrom(testImplementation)
    testintegrationRuntimeOnly.extendsFrom(testRuntimeOnly)
    testperformanceImplementation.extendsFrom(testintegrationImplementation)
    testperformanceRuntimeOnly.extendsFrom(testintegrationRuntimeOnly)
    all {
        resolutionStrategy {
            exclude group: 'org.eclipse.jetty', module: 'jetty-slf4j-impl'
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    api 'org.eclipse.jetty:jetty-server:12.0.15'
    implementation 'org.eclipse.jetty.http3:jetty-http3-server:12.0.15'
    implementation 'org.eclipse.jetty.ee10:jetty-ee10-webapp:12.0.15'
    implementation 'org.eclipse.jetty.ee10:jetty-ee10-servlets:12.0.15'

    api 'jakarta.servlet:jakarta.servlet-api:6.0.0'//Необхадима как транзитивная зависимость для platform
    api 'org.eclipse.jetty.ee10:jetty-ee10-servlet:12.0.15'//Необхадима как транзитивная зависимость для platform

    implementation 'org.eclipse.jetty.websocket:jetty-websocket-jetty-server:12.0.15'
    implementation 'org.eclipse.jetty.ee10.websocket:jetty-ee10-websocket-jetty-server:12.0.15'
    api 'org.eclipse.jetty.websocket:jetty-websocket-jetty-api:12.0.15'
    implementation 'org.eclipse.jetty.ee10.websocket:jetty-ee10-websocket-servlet:12.0.15'

    api 'org.springframework:spring-core:6.1.14'
    api 'org.springframework:spring-web:6.1.14'
    api 'org.springframework:spring-webmvc:6.1.14'
    implementation 'org.springframework:spring-context-support:6.1.14'
    implementation 'org.springframework:spring-messaging:6.1.14'

    implementation 'com.infomaximum:json-smart:2.5.1'

    implementation('org.reflections:reflections:0.10.2') {
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
        exclude group: 'org.javassist', module: 'javassist'
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
    implementation 'org.javassist:javassist:3.30.2-GA'//Более новая зависимость с поддержкой java модулей

    implementation 'org.slf4j:slf4j-api:2.0.16'

    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.11.3'
    testImplementation 'ch.qos.logback:logback-core:1.5.12'
    testImplementation 'ch.qos.logback:logback-classic:1.5.12'

    testintegrationImplementation 'org.springframework:spring-test:6.1.14'
    testintegrationImplementation 'org.eclipse.jetty.websocket:jetty-websocket-jetty-client:12.0.15'
    testintegrationImplementation 'org.eclipse.jetty.ee10.websocket:jetty-ee10-websocket-jakarta-server:12.0.15'
    testintegrationImplementation 'org.eclipse.jetty.http3:jetty-http3-client:12.0.15'
}

task testintegration(type: Test, description: 'Integration test', group: 'Verification') {
    outputs.upToDateWhen { false }
}

task testperformance(type: Test, description: 'Performance test', group: 'Verification') {
    outputs.upToDateWhen { false }
}

tasks.withType(Copy).all { duplicatesStrategy 'exclude' }