group 'com.infomaximum'
version '0.0.16RC1'

apply plugin: 'java'
apply plugin: 'maven'

if (file('publish.gradle').exists()) {
    apply from: 'publish.gradle'
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

sourceSets {
    testintegration {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/testintegration/java')
        }
        resources {
            srcDir file('src/testintegration/resources')
        }
    }
    testperformance {
        java {
            compileClasspath += main.output + test.output + testintegration.output
            runtimeClasspath += main.output + test.output + testintegration.output
            srcDir file('src/testperformance/java')
        }
        resources {
            srcDir file('src/testperformance/resources')
        }
    }
}

configurations {
    testintegrationCompile.extendsFrom testCompile
    testintegrationRuntime.extendsFrom testRuntime
    testperformanceCompile.extendsFrom testintegrationCompile
    testperformanceRuntime.extendsFrom testintegrationRuntime
}

task testintegration(type: Test) {
    testClassesDir = sourceSets.testintegration.output.classesDir
    classpath = sourceSets.testintegration.runtimeClasspath
    outputs.upToDateWhen { false }
}

task testperformance(type: Test) {
    testClassesDir = sourceSets.testperformance.output.classesDir
    classpath = sourceSets.testperformance.runtimeClasspath
    outputs.upToDateWhen { false }
}

dependencies {

    compile 'io.netty:netty-all:4.1.12.Final'

    compile 'org.eclipse.jetty:jetty-server:9.4.11.v20180605'
    compile 'org.eclipse.jetty:jetty-servlet:9.4.11.v20180605'
    compile 'org.eclipse.jetty:jetty-webapp:9.4.11.v20180605'
    compile 'org.eclipse.jetty:apache-jsp:9.4.11.v20180605'
    compile 'org.eclipse.jetty.websocket:websocket-server:9.4.11.v20180605'
    compile 'org.eclipse.jetty.websocket:websocket-servlet:9.4.11.v20180605'

    compile 'org.springframework:spring-core:4.3.9.RELEASE'
    compile 'org.springframework:spring-web:4.3.9.RELEASE'
    compile 'org.springframework:spring-webmvc:4.3.9.RELEASE'
    compile 'org.springframework:spring-context-support:4.3.9.RELEASE'
    compile 'org.springframework:spring-messaging:4.3.9.RELEASE'
    compile 'javax.servlet:jstl:1.2'

    compile 'net.minidev:json-smart:2.3'

    compile 'org.reflections:reflections:0.9.11'

    compile 'org.slf4j:slf4j-api:1.7.25'

    testCompile 'junit:junit:4.12'
    testCompile 'ch.qos.logback:logback-core:1.2.2'
    testCompile 'ch.qos.logback:logback-classic:1.2.3'


    testintegrationCompile 'org.apache.httpcomponents:httpclient:4.5.3'
    testintegrationCompile 'org.springframework:spring-test:4.3.9.RELEASE'
    testintegrationCompile 'org.eclipse.jetty.websocket:websocket-client:9.4.6.v20170531'
}
